class List {
  constructor(size: int) {
    this.inner = [0; size];
  }

  set(index: int, value: int) {
    this.inner[index] = value;
  }

  get(index: int) -> int {
    ret this.inner[index];
  }

  str() -> string {
    let s = "";

    let i = 0;
    //print("size of list is " + len(this.inner));
    until i == len(this.inner) {
      if this.inner[i] == 0 {
        s = s + " ";
      } else {
        s = s + "X";
      }

      i = i + 1;
    }

    ret s;
  }
}

let LIST_SIZE = 100;

let list = List(LIST_SIZE);
list.set(LIST_SIZE - 2, 1);
list.set(LIST_SIZE - 1, 1);

let iter = 0;
until iter == LIST_SIZE {
  print(list.str());
  iter = iter + 1;
  
  //print("creating new list");
  let new_list = List(LIST_SIZE);
  //print("finished creating new list");

  let idx = 0;
  until idx == LIST_SIZE {
    let prev_val = 0;
    if idx != 0 {
      prev_val = list.get(idx - 1);
    }

    let curr_val = list.get(idx);

    let next_val = 0;
    if idx != LIST_SIZE - 1 {
      next_val = list.get(idx + 1);
    }

    let as_str = "" + prev_val + curr_val + next_val;
    let new_val = 0;
    if as_str == "111" {
      new_val = 0;
    } else if as_str == "110" {
      new_val = 1;
    } else if as_str == "101" {
      new_val = 1;
    } else if as_str == "100" {
      new_val = 0;
    } else if as_str == "011" {
      new_val = 1;
    } else if as_str == "010" {
      new_val = 1;
    } else if as_str == "001" {
      new_val = 1;
    } else {
      new_val = 0;
    }

    new_list.set(idx, new_val);

    idx = idx + 1;
  }

  list = new_list;
}
