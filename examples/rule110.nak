class Node {
  constructor(data: int, prev, next) {
    this.data = data;
    this.prev = prev;
    this.next = next;
  }

  str() {
    let str = "";
    if this.data == 0 {
      str = " ";
    } else {
      str = "X";
    }

    if this.next != null {
      str = str + this.next.str();
    }

    ret str;
  }
}

class List {
  constructor(size: int) {
    this.head = Node(0, null, null);

    let curr = this.head;
    
    until size - 1 == 0 {
      size = size - 1;

      let new_node = Node(0, curr, null);
      curr.next = new_node;
      curr = new_node;
    }
  }

  set(index: int, value: int) {
    let curr = this.head;
    until index == 0 {
      index = index - 1;
      curr = curr.next;
    }

    curr.data = value;
  }

  get(index: int) -> int {
    let curr = this.head;
    until index == 0 {
      index = index - 1;
      curr = curr.next;
    }

    ret curr.data;
  }
}

let LIST_SIZE = 100;

let list = List(LIST_SIZE);
list.set(LIST_SIZE - 2, 1);
list.set(LIST_SIZE - 1, 1);

let iter = 0;
until iter == LIST_SIZE {
  print list.head.str();
  iter = iter + 1;
  
  let new_list = List(LIST_SIZE);

  let idx = 0;
  until idx == LIST_SIZE {
    let prev_val = 0;
    if idx != 0 {
      prev_val = list.get(idx - 1);
    }

    let curr_val = list.get(idx);

    let next_val = 0;
    if idx != LIST_SIZE - 1 {
      next_val = list.get(idx + 1);
    }

    let as_str = "" + prev_val + curr_val + next_val;
    let new_val = 0;
    if as_str == "111" {
      new_val = 0;
    } else if as_str == "110" {
      new_val = 1;
    } else if as_str == "101" {
      new_val = 1;
    } else if as_str == "100" {
      new_val = 0;
    } else if as_str == "011" {
      new_val = 1;
    } else if as_str == "010" {
      new_val = 1;
    } else if as_str == "001" {
      new_val = 1;
    } else {
      new_val = 0;
    }

    new_list.set(idx, new_val);

    idx = idx + 1;
  }

  list = new_list;
}
